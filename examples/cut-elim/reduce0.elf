%%% Strongly normalising system for cut elimination
%%% Cut reductions, ignoring side conditions
%%% Author: Frank Pfenning
%%% Implementing Urban & Bierman'01

% Logical cuts
-l-> : # -> # -> type.
%infix none 10 -l->.
%mode -l-> +D -E.

log_not : (cut ([a] notr ([x] M x) a) ([y] notl ([b] N b) y))
	  -l-> (cut ([b] N b) ([x] M x)).
log_and1 : (cut ([b] andr ([a1] M1 a1) ([a2] M2 a2) b)
                ([y] andl1 ([x] N x) y))
          -l-> (cut ([a1] M1 a1) ([x] N x)).
log_and2 : (cut ([b] andr ([a1] M1 a1) ([a2] M2 a2) b)
                ([y] andl2 ([x] N x) y))
          -l-> (cut ([a2] M2 a2) ([x] N x)).

log_or1 : (cut ([b] orr1 ([a] M a) b)
	       ([y] orl ([x1] N1 x1) ([x2] N2 x2) y))
          -l-> (cut ([a] M a) ([x1] N1 x1)).
log_or2 : (cut ([b] orr2 ([a] M a) b)
	       ([y] orl ([x1] N1 x1) ([x2] N2 x2) y))
          -l-> (cut ([a] M a) ([x2] N2 x2)).

log_imp_l : (cut ([b] impr ([x] [a] M x a) b)
	         ([z] impl ([c] N c) ([y] P y) z))
          -l-> (cut ([a] cut ([c] N c) ([x] M x a)) ([y] P y)).
log_imp_r : (cut ([b] impr ([x] [a] M x a) b)
	         ([z] impl ([c] N c) ([y] P y) z))
          -l-> (cut ([c] N c) ([x] cut ([a] M x a) ([y] P y))).
log_ax_r : (cut ([a] M a) ([x] axiom x B))
          -l-> M B.
log_ax_l : (cut ([a] axiom Y a) ([x] M x))
          -l-> M Y.

% Substitution
substp : (pos A -> #) -> (neg A -> #) -> # -> type.
substn : (neg A -> #) -> (pos A -> #) -> # -> type.
%mode substp +D +E -F.
%mode substn +D +E -F.

subst1 : substp ([c] axiom X c) ([y] P y) (P X).
subst2 : substn ([y] axiom y A) ([c] P c) (P A).
subst3 : substp ([a] notr ([x] M a x) a) ([y] P y)
	  (cut ([a] notr ([x] M' x) a) ([y] P y))
	  <- ({x} substp ([a] M a x) ([y] P y) (M' x)).
subst4 : substn ([x] notl ([a] M x a) x) ([c] P c)
	  (cut ([c] P c) ([x] notl ([a] M' a) x))
	  <- ({a} substn ([x] M x a) ([c] P c) (M' a)).
subst5 : substp ([c] andr ([a] M c a) ([b] N c b) c) ([y] P y)
	  (cut ([c] andr ([a] M' a) ([b] N' b) c) ([y] P y))
	  <- ({a} substp ([c] M c a) ([y] P y) (M' a))
	  <- ({b} substp ([c] N c b) ([y] P y) (N' b)).
subst6_1 : substn ([y] andl1 ([x] M y x) y) ([c] P c)
	  (cut ([c] P c) ([y] andl1 ([x] M' x) y))
	  <- ({x} substn ([y] M y x) ([c] P c) (M' x)).
subst6_2 : substn ([y] andl2 ([x] M y x) y) ([c] P c)
	  (cut ([c] P c) ([y] andl2 ([x] M' x) y))
	  <- ({x} substn ([y] M y x) ([c] P c) (M' x)).
subst7_1 : substp ([c] orr1 ([a] M c a) c) ([y] P y)
	  (cut ([c] orr1 ([a] M' a) c) ([y] P y))
	  <- ({a} substp ([c] M c a) ([y] P y) (M' a)).
subst7_2 : substp ([c] orr2 ([a] M c a) c) ([y] P y)
	  (cut ([c] orr2 ([a] M' a) c) ([y] P y))
	  <- ({a} substp ([c] M c a) ([y] P y) (M' a)).
subst8 : substn ([z] orl ([x] M z x) ([y] N z y) z) ([c] P c)
	  (cut ([c] P c) ([z] orl ([x] M' x) ([y] N' y) z))
	  <- ({x} substn ([z] M z x) ([c] P c) (M' x))
	  <- ({y} substn ([z] N z y) ([c] P c) (N' y)).
subst9 : substp ([b] impr ([x] [a] M b x a) b) ([y] P y)
	  (cut ([b] impr ([x] [a] M' x a) b) ([y] P y))
	  <- ({x} {a} substp ([b] M b x a) ([y] P y) (M' x a)).
subst10 : substn ([y] impl ([a] M y a) ([x] N y x) y) ([c] P c)
	   (cut ([c] P c) ([y] impl ([a] M' a) ([x] N' x) y))
	   <- ({a} substn ([y] M y a) ([c] P c) (M' a))
	   <- ({x} substn ([y] N y x) ([c] P c) (N' x)).

% Otherwise (side condition?)
subst11p : substp ([b] axiom X A) ([y] P y) (axiom X A).
subst11n : substn ([y] axiom X A) ([c] P c) (axiom X A).
subst12p : substp ([b] cut ([a] M b a) ([x] N b x)) ([y] P y)
	    (cut ([a] M' a) ([x] N' x))
	    <- ({a} substp ([b] M b a) ([y] P y) (M' a))
	    <- ({x} substp ([b] N b x) ([y] P y) (N' x)).
subst12n : substn ([y] cut ([a] M y a) ([x] N y x)) ([c] P c)
	    (cut ([a] M' a) ([x] N' x))
	    <- ({a} substn ([y] M y a) ([c] P c) (M' a))
	    <- ({x} substn ([y] N y x) ([c] P c) (N' x)).
subst13p : substp ([b] notr ([x] M b x) A) ([y] P y) (notr ([x] M' x) A)
	    <- ({x} substp ([b] M b x) ([y] P y) (M' x)).
subst13n : substn ([y] notr ([x] M y x) A) ([c] P c) (notr ([x] M' x) A)
	    <- ({x} substn ([y] M y x) ([c] P c) (M' x)).
subst14p : substp ([b] notl ([a] M b a) X) ([y] P y) (notl ([a] M' a) X)
	    <- ({a} substp ([b] M b a) ([y] P y) (M' a)).
subst14n : substn ([y] notl ([a] M y a) X) ([c] P c) (notl ([a] M' a) X)
	    <- ({a} substn ([y] M y a) ([c] P c) (M' a)).
subst15p : substp ([d] andr ([a] M d a) ([b] N d b) C) ([y] P y)
	    (andr ([a] M' a) ([b] N' b) C)
	    <- ({a} substp ([d] M d a) ([y] P y) (M' a))
	    <- ({b} substp ([d] N d b) ([y] P y) (N' b)).
subst15n : substn ([y] andr ([a] M y a) ([b] N y b) C) ([d] P d)
	    (andr ([a] M' a) ([b] N' b) C)
	    <- ({a} substn ([y] M y a) ([d] P d) (M' a))
	    <- ({b} substn ([y] N y b) ([d] P d) (N' b)).
subst16p_1 : substp ([c] andl1 ([x] M c x) Y) ([z] P z) (andl1 ([x] M' x) Y)
	      <- ({x} substp ([c] M c x) ([z] P z) (M' x)).
subst16n_1 : substn ([z] andl1 ([x] M z x) Y) ([c] P c) (andl1 ([x] M' x) Y)
	      <- ({x} substn ([z] M z x) ([c] P c) (M' x)).
subst16p_2 : substp ([c] andl2 ([x] M c x) Y) ([z] P z) (andl2 ([x] M' x) Y)
	      <- ({x} substp ([c] M c x) ([z] P z) (M' x)).
subst16n_2 : substn ([z] andl2 ([x] M z x) Y) ([c] P c) (andl2 ([x] M' x) Y)
	      <- ({x} substn ([z] M z x) ([c] P c) (M' x)).
subst17p_1 : substp ([c] orr1 ([a] M c a) B) ([y] P y) (orr1 ([a] M' a) B)
	      <- ({a} substp ([c] M c a) ([y] P y) (M' a)).
subst17n_1 : substn ([y] orr1 ([a] M y a) B) ([c] P c) (orr1 ([a] M' a) B)
	      <- ({a} substn ([y] M y a) ([c] P c) (M' a)).
subst17p_2 : substp ([c] orr2 ([a] M c a) B) ([y] P y) (orr2 ([a] M' a) B)
	      <- ({a} substp ([c] M c a) ([y] P y) (M' a)).
subst17n_2 : substn ([y] orr2 ([a] M y a) B) ([c] P c) (orr2 ([a] M' a) B)
	      <- ({a} substn ([y] M y a) ([c] P c) (M' a)).
subst18p : substp ([c] orl ([x] M c x) ([y] N c y) Z) ([w] P w)
	    (orl ([x] M' x) ([y] N' y) Z)
	    <- ({x} substp ([c] M c x) ([w] P w) (M' x))
	    <- ({y} substp ([c] N c y) ([w] P w) (N' y)).
subst18n : substn ([w] orl ([x] M w x) ([y] N w y) Z) ([c] P c)
	    (orl ([x] M' x) ([y] N' y) Z)
	    <- ({x} substn ([w] M w x) ([c] P c) (M' x))
	    <- ({y} substn ([w] N w y) ([c] P c) (N' y)).
subst19p : substp ([c] impr ([x] [a] M c x a) B) ([y] P y)
	    (impr ([x] [a] M' x a) B)
	    <- ({x} {a} substp ([c] M c x a) ([y] P y) (M' x a)).
subst19n : substn ([y] impr ([x] [a] M y x a) B) ([c] P c)
	    (impr ([x] [a] M' x a) B)
	    <- ({x} {a} substn ([y] M y x a) ([c] P c) (M' x a)).
subst20p : substp ([c] impl ([a] M c a) ([x] N c x) Y) ([z] P z)
	    (impl ([a] M' a) ([x] N' x) Y)
	    <- ({a} substp ([c] M c a) ([z] P z) (M' a))
	    <- ({x} substp ([c] N c x) ([z] P z) (N' x)).
subst20n : substn ([z] impl ([a] M z a) ([x] N z x) Y) ([c] P c)
	    (impl ([a] M' a) ([x] N' x) Y)
	    <- ({a} substn ([z] M z a) ([c] P c) (M' a))
	    <- ({x} substn ([z] N z x) ([c] P c) (N' x)).

% Commuting cuts
-c-> : # -> # -> type.
%infix none 10 -c->.
%mode -c-> +D -E.

% side condition: M does not freshly introduce a?
com_l : (cut ([a] M a) ([x] N x)) -c-> P
	 <- substp ([a] M a) ([x] N x) P.

% side condition: N does not freshly introduce x?
com_r : (cut ([a] M a) ([x] N x)) -c-> P
	 <- substn ([x] N x) ([a] M a) P.
